buildscript {
    ext {
        set('springBootVersion', "2.2.2.RELEASE")
    }
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

//所有工程的配置
allprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    group = 'com.xiaoze.springcloud'
    sourceCompatibility = '1.8'

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
}

//子项目的公共配置
subprojects {

    ext {
        set('springCloudVersion', "Hoxton.SR1")
        set('commonsLang3Version', "3.9")
        set('commonsCollections4', "4.4")
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        implementation "org.apache.commons:commons-lang3:${commonsLang3Version}"
        implementation "org.apache.commons:commons-collections4:${commonsCollections4}"

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        annotationProcessor 'org.projectlombok:lombok:1.18.10'
        compileOnly 'org.projectlombok:lombok:1.18.10'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'
        testCompileOnly 'org.projectlombok:lombok:1.18.10'

    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    configurations {
        providedRuntime
        // remove default logger
        all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}